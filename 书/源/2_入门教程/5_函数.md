# 函数

**函数** (function) 是一小段代码。
函数的主要作用有 **重用** 和 **封装** (抽象)。

重用 (重复使用) 就是在多个地方, 多次使用同一个函数 (也就是使用同一段代码)。
编程时有一个很好的原则: **不要重复自己** (DRY)。
简单来说, 就是相同的或相似的一段代码, 尽量不要出现 3 次以上。
如果发现自己写了很多次相同或相似的代码, 就要思考一下,
是不是可以把这些代码改写成一个函数 ?


## 定义函数

我们来定义一个函数, 比如:

```
定义测试:
  输出 666。

输出 222。
```

这里我们定义了一个函数, 函数的名称是 `测试`,
函数体 (也就是函数内的一段代码) 有 1 行 (第 2 行)。

函数体使用缩进表示其范围, 每一级缩进使用 2 个空格。

函数中的代码在定义时是 **不会执行** 的, 所以运行上面这段代码, 只会输出 `222`。

----

歧语言中定义函数的语法格式为:

```
定义名称(返回类型), 参数列表, 泛型列表:
  # 函数体
```

参数和泛型都是可选的, 如果省略返回类型, 表示函数不返回数据。


## 调用函数

如果想要执行函数中的代码, 就要 **调用** 函数, 比如:

```
定义测试:
  输出 666。
  输出 555。

输出 2。
测试。  # 调用函数

输出 3。
测试。  # 再次调用
```

函数可以在多个地方多次调用, 这就是函数的主要用处。
每调用一次, 函数内的代码就会执行一次。

### 在函数中调用函数

一个函数也可以调用别的函数, 比如:

```
定义分隔线:
  输出 "========== 窝是分隔线 =========="。

定义测试:
  分隔线。
  输出 666。
  分隔线。

输出 222。
测试。
分隔线。
```

一个函数甚至还可以调用自己 (**递归** 调用)。
递归调用需要注意结束条件, 否则可能变成死循环。


## 函数的参数

上面使用的是没有参数 (0 个参数) 的函数,
其实函数也是可以有参数的, 比如:

```
定义计算, 参数x(数字):
  输出(x + 1)。

计算 1。  # 调用函数, 并传递参数
计算 2。
```

这里的函数 `计算` 具有 1 个参数, 参数名称是 `x`, 参数的数据类型是 `数字`。
函数的所有参数都必须标明数据类型。

调用有参数的函数的时候, 需要传递相应的参数。

函数的参数也是一种变量, 传递参数类似于变量赋值。

### 多个参数

函数也可以有多个参数, 比如:

```
定义计算, 参数x(数字)、y(数字):
  输出(x + y)。

计算 1、2。
计算 42、38。
```

函数的多个参数之间以 `、` (顿号) 分隔。


## 函数的返回

函数也可以有返回值, 比如:

```
定义计算(数字), 参数x(数字):
  x + 1  # 自动返回

设 x = 计算 2
输出x。
```

首先, 需要指定函数返回值的数据类型。
函数最后一个表达式的值会自动返回。

然后可以把函数返回的数据存储在变量中 (赋值)。

也可以手动返回数据, 比如:

```
定义取值(数字):
  返回 2。

设 x = 取值
输出x。
```

相当于:

```
定义取值(数字):
  2

设 x = 取值
输出x。
```

`取值` 是一个没有参数但是有返回值的函数, 返回的数据类型是 `数字`。

### 提前返回

在函数的执行中可以提前返回, 比如:

```
定义测试:
  输出 222。
  输出 333。
  返回。  # 此处
  输出 444。
  输出 555。

输出 "==== 标记 1"。
测试。
输出 "==== 标记 2"。
```

一旦返回, 函数中剩下的代码就不会再执行。


## 封装

封装是一种方法, 就是把底层的具体细节给隐藏起来,
使用者无需关心具体的内部细节, 直接使用就可以了。

比如我们可以写一个复杂的函数, 这个函数就是封装的一种具体形式。
使用这个函数的人, 只需简单的调用这个函数, 就能达到想要的效果,
而不用知道这个函数具体是怎么实现的。

把很多个有用的函数集中起来, 就得到了 **函数库** (library)。
比如 Windows 操作系统中的 `.dll` 文件, GNU/Linux 操作系统中的 `.so` 文件,
就是一种函数库。

### 标准库

大部分编程语言都有自己的标准库, 里面含有每个程序都能使用的东西。

歧语言的标准库名称为 `标准库`, 也就是歧语言自带 (内置) 的一个函数库,
每个歧语言程序都可以使用标准库里面的东西。

比如我们经常使用的 `输出`, 其是就是 `标准库` 中的一个函数:

```
定义输出, 参数东西(文本), 标记内部。
```

把东西输出到屏幕上, 具体的完整过程是很复杂的,
但是使用这个函数的人不需要关心这些, 直接调用就好。


## 自相似 (可跳过)

也就是部分和整体的相似性。

一个函数和整个程序, 是相似的。
程序具有参数 (输入数据), 执行结果 (输出数据), 也可能执行到一半提前终止 (退出)。
函数具有参数 (输入数据), 返回值 (输出数据), 也可能执行一半提前终止 (提前返回)。
